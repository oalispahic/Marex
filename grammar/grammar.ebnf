PROGRAM		::= {STATEMENT}

STATEMENT      ::= VAR_DECL
                 | ASSIGN
                 | PRINT
                 | IF_STMT
                 | LOOP_STMT
                 | EXPR_STMT



VAR_DECL	::= "new" IDENT ":=" EXPR
ASSIGN 		::= IDENT ":=" EXPR
PRINT 		::= "print" "(" EXPR ")"

IF_STMT        ::= "if" "(" EXPR ")"
                   { STATEMENT }
                   [ "else" { STATEMENT } ]
                   "fi"


LOOP_STMT   ::= FOR_LOOP | RANGE_LOOP

RANGE_LOOP     ::= "loop" "(" IDENT "->" IDENT ")"
                   { STATEMENT }
                   "done"


FOR_LOOP       ::= "loop" "(" [ VAR_DECL ] ";" EXPR ";" ASSIGN ")"
                   { STATEMENT }
                   "done"



EXPR            ::= LOGIC_OR

LOGIC_OR        ::= LOGIC_AND { "or" LOGIC_AND }
LOGIC_AND       ::= EQUALITY  { "and" EQUALITY }

EQUALITY        ::= COMPARISON { ("==" | "!=") COMPARISON }

COMPARISON      ::= TERM { ("<" | ">") TERM }

TERM            ::= FACTOR { ("+" | "-") FACTOR }

FACTOR          ::= UNARY { ("*" | "/") UNARY }

UNARY           ::= "-" UNARY
                  | PRIMARY

PRIMARY         ::= NUMBER
                  | STRING
                  | IDENT
                  | "(" EXPR ")"
